plugins {
  id 'java'
  id 'maven-publish'
  id 'com.github.jruby-gradle.base' version "${jrubyGradlePluginVersion}"
}

apply from: "$rootDir/gradle/any/publishing.gradle"
apply from: "$rootDir/gradle/any/java.gradle"

ext.title = 'Jekyll Gems'
project.description = 'JRuby environment for running Jekyll to generate Unidata documentation sets.'

sourceSets {
  main {
    output.dir(tasks.jrubyPrepare.outputDir)
  }
}

classes.dependsOn tasks.getByName('jrubyPrepare')

repositories {
  jcenter() // com.github.jruby-gradle.base plugin
  ruby.gems()
  // only the unidata theme and plugin should come from the unidata artifacts server
  exclusiveContent {
    forRepository {
      maven {
        url "https://artifacts.unidata.ucar.edu/repository/unidata-all/"
      }
    }
    filter {
      includeModule 'rubygems', 'unidata-jekyll-theme'
      includeModule 'rubygems', 'unidata-jekyll-plugins'
    }
  }
}

dependencies {
  // jekyll 4.x moved to using sassc (relies on c extension code) over sass (pure ruby version)
  // We will stay on jekyll 3.x until sassc works with jruby
  // see https://github.com/sass/sassc-ruby/issues/182
  gems 'rubygems:jekyll:3.8.7'
  gems 'rubygems:minima:2.5.1'
  gems 'rubygems:jekyll-feed:0.15.1'
  gems 'rubygems:jekyll-seo-tag:2.7.1'
  gems "rubygems:unidata-jekyll-theme:${unidataThemeVersion}"
  gems "rubygems:unidata-jekyll-plugins:${unidataThemeVersion}"
}

publishing {
  publications {
    gemJar(MavenPublication) {
      artifact jar
      pom {
        description = project.description
        url = project.url
      }
    }
  }
}
